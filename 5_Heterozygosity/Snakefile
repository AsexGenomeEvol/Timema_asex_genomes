import re

timema = ['1_Tdi', '1_Tps',
          '2_Tcm', '2_Tsi',
          '3_Tce', '3_Tms',
          '4_Tbi', '4_Tte',
          '5_Tge', '5_Tpa']

trimmed_reads = []
mapping_indices = []
manta_dirs = []

with open('tables/resequencing_samples.tsv') as tab :
	tab.readline()
	for textline in tab :
		line = textline.split()
		trimmed_reads.append("data/" + line[1] + "/trimmed_reads/" + line[2])
		mapping_indices.append("data/" + line[1] + "/mapping/" + line[2] + "_to_b3v08.bam.bai")
		manta_dirs.append("data/" + line[1] + "/variant_calls/" + line[2] + "/" + line[2] + "_manta")

### if environmental variable USE_LOCAL contains anything, it will compute on /scratch/local
cluster_script = os.environ.get("USE_LOCAL")
if cluster_script == None :
	cluster_script = ""
else :
	cluster_script = "scripts/use_local.sh "

#### calling rules, in the end there should I one that does all,
#### but in practice it's useful to be able to call the individual steps

rule trim_all :
	input :
		expand(trimmed_reads)

rule map_all :
	input :
		expand(mapping_indices)

rule manta_all :
	input :
		expand(manta_dirs)

#### the rules of individual steps

rule trim_reads :
	threads : 16
	resources : mem=80000000, tmp=150000
	input : "data/Illumina-PE_adapters.fa", "data/{sp}/raw_reads/{sample}"
	output : "data/{sp}/trimmed_reads/{sample}"
	shell : cluster_script + "B_SV_calling/trim_reads.sh {wildcards.sp} {wildcards.sample} {input} {output}"

rule index_genome :
	threads : 1
	resources: mem=20000000, tmp=20000
	input : "data/{sp}/reference/{sp}_b3v08.fasta.gz"
	output : "data/{sp}/reference/{sp}_b3v08.fasta.gz.bwt"
	shell : cluster_script + "B_SV_calling/index_fa_bwa.sh {input} data/{wildcards.sp}/reference/{wildcards.sp}_b3v08.fasta.gz."

rule map_reads :
	threads : 16
	resources : mem=104857600, tmp=60000
	input : "data/{sp}/reference/{sp}_b3v08.fasta.gz.bwt", "data/{sp}/trimmed_reads/{sample}"
	output : "data/{sp}/mapping/{sample}_to_b3v08.bam"
	shell :
		cluster_script + "B_SV_calling/map_reads.sh {wildcards.sample} {wildcards.sp} data/{wildcards.sp}/trimmed_reads/{wildcards.sample}/*_R[12]*q.gz data/{wildcards.sp}/reference/{wildcards.sp}_b3v08.fasta.gz.* {output}"

rule index_bam :
	threads : 1
	resources : mem=20000000, tmp=20000
	input : "{bam}.bam"
	output : "{bam}.bam.bai"
	shell : cluster_script + "B_SV_calling/index_bam.sh {input} {output}"

rule filter_bam :
	threads : 1
	resources : mem=20000000, tmp=30000
	input : "data/{sp}/mapping/{sample}_to_b3v08.bam", "data/{sp}/mapping/{sample}_to_b3v08.bam.bai"
	output : "data/{sp}/mapping/{sample}_to_b3v08_mapped_within_scfs.bam"
	shell : cluster_script + "B_SV_calling/filter_reads.sh B_SV_calling/filter_splitreads.py {input} {output}"

rule index_fasta :
	threads : 1
	resources : mem=20000000, tmp=10000
	input : "data/{sp}/reference/{sp}_b3v08.fasta.gz"
	output : "data/{sp}/reference/{sp}_b3v08.fasta.gz.fai", "data/{sp}/reference/{sp}_b3v08.fasta.gz.gzi"
	shell : cluster_script + "B_SV_calling/index_fa_sam.sh {input} {output}"

rule call_manta :
	threads : 32
	resources : mem=200000000, tmp=50000
	input : "data/{sp}/reference/{sp}_b3v08.fasta.gz", "data/{sp}/reference/{sp}_b3v08.fasta.gz.fai", "data/{sp}/reference/{sp}_b3v08.fasta.gz.gzi", "data/{sp}/mapping/{sample}_to_b3v08_mapped_within_scfs.bam", "data/{sp}/mapping/{sample}_to_b3v08_mapped_within_scfs.bam.bai"
	output : directory("data/{sp}/variant_calls/{sample}/{sample}_manta")
	shell : cluster_script + "B_SV_calling/manta.sh {input} {output}"
